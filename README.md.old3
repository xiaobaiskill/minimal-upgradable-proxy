minimal upgradable proxy (slot 0)
---


### create upgradable proxy contract
```
# store logic address to slot of proxy contract
PUSH20 <logicAddress>  [logicAddress]
RETURNDATASIZE         [slot0 logicAddress]
SSTORE                 []                  => storage(slot0 => logicAddress)

# return deployedCode
PUSH1 0x1a             [0x1a]
PUSH1 0x21             [0x21 0x1a]
RETURNDATASIZE         [00 0x21 0x1a]
CODECOPY               []          ==> memory(0x00~0x1a: 0x21~0x3b(deployedCode1stPart))
PUSH1 0x1a             [0x1a]
RETURNDATASIZE         [00 0x1a]
RETURN


# proxy contract (deployedcode)
CALLDATASIZE        [calldatasize] 
RETURNDATASIZE      [00 calldatasize]
RETURNDATASIZE      [00 00 calldatasize]
CALLDATACOPY        []     ==> memory(00~calldatasize => codedata)
RETURNDATASIZE      [00]
RETURNDATASIZE      [00 00]
RETURNDATASIZE      [00 00 00]
CALLDATASIZE        [calldatasize 00 00 00]
RETURNDATASIZE      [00 calldatasize 00 00 00]
RETURNDATASIZE      [00 00 calldatasize 00 00 00] 
SLOAD               [logicAddress 00 calldatasize 00 00 00]
GAS                 [gas logicAddress 00 calldatasize 00 00 00]
DELEGATECALL        [result 00]
RETURNDATASIZE      [returnDataSize result 00]
DUP3                [00 returnDataSize result 00]
DUP1                [00 00 returnDataSize result 00]
RETURNDATACOPY      [result 00] => memory(00~RETURNDATASIZE => RETURNDATA)
RETURNDATASIZE      [returnDataSize result 00] 
DUP3                [00 returnDataSize result 00] 
DUP3                [result 00 returnDataSize result 00]
PUSH1 0x18          [0x18 result 00 returnDataSize result 00]
JUMPI				[00 returnDataSize result 00]
REVERT              [result 00]
JUMPDEST            [00 returnDataSize result 00]
RETURN              [result 00]
```


* bytecode
replace `yyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyy` to a address of 20bytes before deploying contract 

```
73yyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyyy3d55601a60213d39601a3df3363d3d373d3d3d363d3d545af43d82803e3d8282601857fd5bf3
```

* deployedcode 
wherein the bytes at indices 10 - 41 (inclusive) are replaced with the 32 byte slot of the master after created
```
363d3d373d3d3d363d3d545af43d82803e3d8282601857fd5bf3
```
